# anzhuoliulanqi
简易浏览器（易语言制作）
# 搭建主要框架
![image](https://github.com/anzhuo114514/anzhuoliulanqi/assets/131332039/f52f78b9-6c34-4a9d-9943-a5efafc6aad1)
# 搭建取消命令
![image](https://github.com/anzhuo114514/anzhuoliulanqi/assets/131332039/2dc0f69b-c0f2-4715-b813-e1aa8115bf63)
#创建超文本浏览框1_即将打开新窗口（逻辑）
图片无法查看（忘记拍了）
#其他源码
![image](https://github.com/anzhuo114514/anzhuoliulanqi/assets/131332039/19389c26-b99d-499b-a2e5-fc40396a7ec0)
.版本 2
.支持库 HtmlView

.程序集 窗口程序集1

.子程序 _按钮1_被单击
.局部变量 临时浏览框, 超文本浏览框















临时浏览框 ＝ 取标记组件 (组合框1.取项目数值 (组合框1.现行选中项))
临时浏览框.地址 ＝ 编辑框1.内容

.子程序 __启动窗口_尺寸被改变
.局部变量 临时浏览器, 超文本浏览框

临时浏览器 ＝ 取标记组件 (组合框1.取项目数值 (组合框1.现行选中项))
临时浏览器.高度 ＝ _启动窗口.高度 － 84
临时浏览器.宽度 ＝ _启动窗口.宽度 － 8
超文本浏览框1.高度 ＝ _启动窗口.高度 － 84
超文本浏览框1.宽度 ＝ _启动窗口.宽度 － 8
编辑框1.宽度 ＝ _启动窗口.宽度 － 60
按钮2.左边 ＝ 编辑框1.宽度 ＋ 30
组合框1.宽度 ＝ _启动窗口.宽度 － 154
按钮1.左边 ＝ 编辑框1.宽度 ＋ 2
标签1.左边 ＝ 组合框1.宽度 ＋ 72

.子程序 __启动窗口_创建完毕
.局部变量 命令, 文本型, , "1"

取命令行 (命令)
全选 ＝ 真
组合框1.加入项目 (“正在载入中...........”, 1)
组合框1.现行选中项 ＝ 0
现行项目 ＝ 0
编号 ＝ 1
.如果 (取数组下标 (命令, ) ＞ 4)
    编辑框1.内容 ＝ 命令 [5]
    _启动窗口.宽度 ＝ 到数值 (命令 [1])
    _启动窗口.高度 ＝ 到数值 (命令 [2])
    _启动窗口.左边 ＝ 到数值 (命令 [3])
    _启动窗口.顶边 ＝ 到数值 (命令 [4])
    选择框1.选中 ＝ 假
.否则
    ' 本源码来自anzhuo114514（GitHub）
.如果结束
_按钮1_被单击 ()

.子程序 _超文本浏览框1_即将打开新窗口, 逻辑型
.局部变量 临时浏览器, 超文本浏览框
.局部变量 临时浏览框, 超文本浏览框

临时浏览框 ＝ 取事件组件 ()
.如果 (选择框1.选中)
    复制窗口组件 (超文本浏览框1, 临时浏览器)
    编号 ＝ 编号 ＋ 1
    临时浏览器.标记 ＝ 到文本 (编号)
    临时浏览器.高度 ＝ _启动窗口.高度 － 84
    临时浏览器.宽度 ＝ _启动窗口.宽度 － 8
    组合框1.加入项目 (“正在载入中。。。。。。”, 编号)
    组合框1.现行选中项 ＝ 组合框1.取项目数 () － 1
    临时浏览器.地址 ＝ 临时浏览框.状态条文本
    _组合框1_列表项被选择 ()
.否则
    运行 (取运行目录 () ＋ “\” ＋ 取执行文件名 () ＋ “ ” ＋ 到文本 (_启动窗口.宽度) ＋ “ ” ＋ 到文本 (_启动窗口.高度) ＋ “ ” ＋ 到文本 (_启动窗口.左边) ＋ “ ” ＋ 到文本 (_启动窗口.顶边) ＋ “ ” ＋ 临时浏览框.状态条文本, 假, )
.如果结束
返回 (假)
' 本源码来自anzhuo114514(GitHub)

.子程序 _超文本浏览框1_载入完毕
.局部变量 临时浏览器, 超文本浏览框

临时浏览器 ＝ 取事件组件 ()
组合框1.置项目文本 (寻找数值 (到数值 (临时浏览器.标记)), 临时浏览器.标题)
编辑框1.内容 ＝ 临时浏览器.地址

.子程序 _组合框1_列表项被选择
.局部变量 临时浏览器, 超文本浏览框

.如果真 (现行项目 ≠ 组合框1.现行选中项)
    .如果真 (现行项目 ≥ 0)
        临时浏览器 ＝ 取标记组件 (组合框1.取项目数值 (现行项目))
        临时浏览器.可视 ＝ 假
    .如果真结束
    临时浏览器 ＝ 取标记组件 (组合框1.取项目数值 (组合框1.现行选中项))
    临时浏览器.可视 ＝ 真
    临时浏览器.高度 ＝ _启动窗口.高度 － 84
    临时浏览器.宽度 ＝ _启动窗口.宽度 － 8
    现行项目 ＝ 组合框1.现行选中项
    编辑框1.内容 ＝ 临时浏览器.地址
    标签1.标题 ＝ 到文本 (组合框1.现行选中项 ＋ 1) ＋ “/” ＋ 到文本 (组合框1.取项目数 ())
.如果真结束


.子程序 _按钮2_被单击
.局部变量 临时浏览框, 超文本浏览框
.局部变量 临时, 整数型

临时浏览框 ＝ 取标记组件 (组合框1.取项目数值 (组合框1.现行选中项))
临时 ＝ 组合框1.现行选中项 － 1
.如果 (组合框1.取项目数 () ＞ 1)
    临时浏览框.销毁 ()
    组合框1.删除项目 (组合框1.现行选中项)
    .如果真 (临时 ＜ 0)
        临时 ＝ 0
    .如果真结束
    组合框1.现行选中项 ＝ 临时
    现行项目 ＝ -1
    _组合框1_列表项被选择 ()
.否则
    组合框1.置项目文本 (0, “未打开网页”)
    临时浏览框.地址 ＝ “about:blank”
    编辑框1.内容 ＝ “about:blank”
.如果结束


.子程序 寻找数值, 整数型
.参数 目标, 整数型
.局部变量 循环, 整数型

循环 ＝ 0
.判断循环首 (循环 ＜ 组合框1.取项目数 ())
    .如果真 (组合框1.取项目数值 (循环) ＝ 目标)
        跳出循环 ()
    .如果真结束
    循环 ＝ 循环 ＋ 1
.判断循环尾 ()
返回 (循环)


.子程序 _编辑框1_按下某键, 逻辑型
.参数 键代码, 整数型
.参数 功能键状态, 整数型

.如果真 (键代码 ＝ #回车键)
    _按钮1_被单击 ()
.如果真结束


.子程序 _编辑框1_鼠标左键被放开, 逻辑型
.参数 横向位置, 整数型
.参数 纵向位置, 整数型
.参数 功能键状态, 整数型

.如果真 (全选)
    编辑框1.被选择字符数 ＝ -1
    全选 ＝ 假
.如果真结束


.子程序 _编辑框1_失去焦点

全选 ＝ 真

# 预览

严谨抄袭，谢谢（灵感来自于易语言资源网）
